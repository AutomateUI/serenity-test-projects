group = 'net.serenity-bdd'
apply plugin: "java"

repositories {
    mavenLocal()
    jcenter()
}

ext {
    serenityCoreVersion = "1.1.25-SNAPSHOT"
    serenityJBehaveVersion = "1.5.0"
    serenityCucumberVersion = "1.1.2"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

gradle.startParameter.continueOnFailure = true


subprojects {
    apply plugin: "java"

    configurations.all {
        def dependencies = [
                "net.serenity-bdd:serenity-gradle-plugin": "${serenityCoreVersion}",
                "net.serenity-bdd:serenity-core"         : "${serenityCoreVersion}",
                "net.serenity-bdd:serenity-junit"        : "${serenityCoreVersion}",
                "net.serenity-bdd:serenity-journey"      : "${serenityCoreVersion}",
                "net.serenity-bdd:browse-the-web"        : "${serenityCoreVersion}"]
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (dependencies["${details.requested.group}:${details.requested.name}"]) {
                details.useVersion dependencies["${details.requested.group}:${details.requested.name}"]
                println "dependecy replaced to: ${details.requested.group}:${details.requested.name}:${dependencies["${details.requested.group}:${details.requested.name}"]}"
            }
        }
    }

    buildscript {
        ext {
            serenityGradlePluginVersion = "1.1.25-SNAPSHOT"
        }
        repositories {
            mavenLocal()
            jcenter()
        }
        configurations.all {
            def dependencies = [
                    "net.serenity-bdd:serenity-gradle-plugin": "${serenityGradlePluginVersion}"]
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                if (dependencies["${details.requested.group}:${details.requested.name}"]) {
                    details.useVersion dependencies["${details.requested.group}:${details.requested.name}"]
                    println "dependecy replaced to: ${details.requested.group}:${details.requested.name}:${dependencies["${details.requested.group}:${details.requested.name}"]}"
                }
            }
        }
    }

    task subProjectsCopyDeps(type: Copy) {
        from configurations.runtime
        into project.projectDir.path + "/lib"
    }
}
